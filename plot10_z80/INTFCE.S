	GLOBAL ADEIN,ADEOUT,KAS2A1,KAS2AM,KA12AS,KAM2AS
SYSTEM:	EQU 1403H
CONIN:	EQU	1
CONOUT:	EQU	2
WRITEB:	EQU	0EH	;WRITE BINARY
READL:	EQU	0CH	;READ LINE
;
;___________________________________________
ADEIN:
	;H[MTAR ADE ARRAY
	;  HL PEKAR P] ANTAL TECKEN
	;  DE PEKAR P] ADE ARRAY
	;
	PUSH IY
	PUSH HL
	PUSH DE
	LD C,(HL)
	INC HL
	LD B,(HL)
	LD DE,INBUFF
	LD IY,IYIBUF		;S[TT UPP IY-VEKTOR
	LD (IY),CONIN		;ENHET
	LD (IY+1),READL		;TYP AV \VERF\RING
	LD (IY+2),E		;BUFFER-ADRESS
	LD (IY+3),D
	LD (IY+4),C		;ANTAL TECKEN
	LD (IY+5),B
	XOR A			;NOLLST[LL RESTEN
	LD (IY+6),A
	LD (IY+7),A
	LD (IY+8),A
	LD (IY+9),A
	CALL SYSTEM		;STARTA \VERF\RING
	;
	LD HL,INBUFF		;SKALA BORT BLANKA
				;FRAM TILL <CR>
	LD C,(IY+4)
	LD B,(IY+5)
	ADD HL,BC		;ADRESS SISTA TECKEN
	DEC HL
STRIP1:	DEC HL
	DEC BC
	XOR A			;ANTAL TECKEN > 0 ?
	CP C
	JR Z,ENSTRP
	LD A,(HL)		;BLANKA ?
	AND 7FH			;SKALA AV PARITET
	CP ' '
	JR Z,STRIP1		;FORTS[TT TAG BORT
				;BLANKA
ENSTRP:
	POP DE			;STARTADRESS ADE ARRAY
	PUSH DE
	PUSH BC
	LD B,C			;ANTAL TECKEN
	LD HL,INBUFF
	CALL STRADE		;OMVANDLA TILL ADE ARRAY
	POP BC			;S[TT UTPARAMETRAR
	POP DE
	POP HL
	LD (HL),C
	INC HL
	LD (HL),B
	DEC HL
	POP IY
	RET
;__________________________________________
STRADE:
	;\VERS[TT STR[NG TILL ADE ARRAY
	;  HL PEKAR P] STR[NG
	;  DE PEKAR P] ADE ARRAY
	;  B ANGER ANTAL TECKEN  > 0 , < 256
	;
	XOR A			;KOLLA L[NGDEN > 0
	CP B
	RET Z
STRLOP:	LD A,(HL)		;FLYTTA STR[NGEN
	AND 7FH			;BORT MED PARITET
	LD (DE),A
	INC DE
	XOR A
	LD (DE),A		;NOLLST[LL H\GA DELEN
	INC DE
	INC HL
	DJNZ STRLOP		;LOOP TILLS ALLA TECKEN
				;\VERF\RDA
	RET
;_______________________________________________________
ADEOUT:
	;S[NDER UT ADE ARRAY
	; HL PEKAR P] ANTAL TECKEN
	; DE PEKAR P] ADE ARRAY
	;
	PUSH IY
	XOR A			;H[MTA PARAMETRAR
	LD C,A
	LD B,(HL)		;ANTAL TECKEN
	CP B
	JR Z,ADORET		;ANTAL TECKEN = 0 ?
	LD H,D			;ADE STARTADRESS
	LD L,E
	LD DE,UTBUFF		;STR[NG ADRESS
	PUSH DE
	PUSH BC
	CALL ADESTR		;G\R OM TILL STR[NG
	POP BC
	POP DE
	LD IY,IYOBUF		;LADDA VEKTOR
	LD (IY),CONOUT		;ENHET
	LD (IY+1),WRITEB	;WRITE BINARY
	LD (IY+2),E		;STARTADRESS STR[NG
	LD (IY+3),D
	LD (IY+4),B		;L[NGD STR[NG
	LD (IY+5),C
	XOR A			;NOLLST[LL RESTEN
	LD (IY+6),A
	LD (IY+7),A
	LD (IY+8),A
	LD (IY+9),A
	CALL SYSTEM		;S[ND UT DATA
ADORET:
	POP IY
	RET
;_________________________________________________
ADESTR:
	;\VERS[TT ADE ARRAY TILL STR[NG
	;  HL PEKAR P] ADE ARRAY
	;  DE PEKAR P] STR[NG
	;  B ANGER ANTAL TECKEN > 0 , < 256
	;
	XOR A			;L[NGDEN = 0 ?
	CP B
	RET Z
ADELOP:	LD A,(HL)		;\VERF\R TECKEN
	AND 7FH			;PARITET BORT
	LD (DE),A
	INC DE
	INC HL
	INC HL
	DJNZ ADELOP		;\VERF\R TILLS
				;TECKEN SLUT
	RET
;__________________________________________________
KAS2A1:
	;\VERS[TT ADE ARRAY TILL A1 ARRAY
	;  HL PEKAR P] ANTAL TECKEN < 256
	;  DE PEKAR P] ADE ARRAY
	;  BC PEKAR P] A1 ARRAY
	;
	LD A,(HL)
	LD L,E
	LD H,D
	LD D,B
	LD E,C
	LD C,0
	LD B,A
	CALL ADEA1
	RET
;__________________________________________________
ADEA1:
	;\VERS[TT ADE ARRAY TILL A1 ARRAY
	;  HL PEKAR P] ADE ARRAY
	;  DE PEKAR P] A1 ARRAY
	;  B ANGER ANTAL TECKEN > 0 , < 256
	;
	XOR A			;ANTAL TECKEN = 0 ?
ADA1L:	XOR A			;NOLLST[LL L]G BYTE
	LD (DE),A
	INC DE
	LD A,(HL)		;FLYTTA TECKEN
	AND 7FH			;PARITET BORT
	LD (DE),A
	INC DE
	INC HL
	INC HL
	DJNZ ADA1L		;FLYTTA ALLA TECKEN
	RET
;__________________________________________________
KAS2AM:
	;\VERS[TT ADE ARRAY TILL A2 ARRAY
	;  HL PEKAR P] ANTAL TECKEN
	;  DE PEKAR P] ADE ARRAY
	;  BC PEKAR P] A2 ARRAY
	;
	LD A,(HL)
	LD L,E
	LD H,D
	LD D,B
	LD E,C
	LD C,0
	LD B,A
	CALL ADEA2		;\VERS[TT
	RET
;__________________________________________________
ADEA2:
	;\VERS[TT ADE ARRAY TILL A2 ARRAY
	;  HL PEKAR P] ADE ARRAY
	;  DE PEKAR P] A2 ARRAY
	;  B ANGER ANTAL TECKEN
	;
	XOR A 			;ANTAL TECKEN = 0 ?
	CP B
	RET Z
ADA2L:	INC DE
	LD A,(HL)		;FLYTTA TECKEN H\GA DELEN
	AND 7FH			;PARITET BORT
	LD (DE),A
	DEC DE
	INC HL
	INC HL
	LD A,(HL)		;FLYTTA TECKEN L]GA DELEN
	AND 7FH			;PARITET BORT
	LD (DE),A
	INC DE
	INC DE
	INC HL
	INC HL
	DJNZ ADA2L		;FORTS[TT \VERF\RINGEN
	RET
;_____________________________________________________
KA12AS:
	;\VERS[TT A1 ARRAY TILL ADE ARRAY
	;  HL PEKAR P] ANTAL TECKEN
	;  DE PEKAR P] A1 ARRAY
	;  BC PEKAR P] ADE ARRAY
	;
	LD A,(HL)
	LD L,E
	LD H,D
	LD E,C
	LD D,B
	LD C,0
	LD B,A
	CALL A1ADE
	RET
;__________________________________________________
A1ADE:
	;\VERS[TT A1 ARRAY TILL ADE ARRAY
	;  HL PEKAR P] A1 ARRAY
	;  DE PEKAR P] ADE ARRAY
	;  B ANGER ANTAL TECKEN > 0 , < 256
	;
	XOR A			;ANTAL TECKEN = 0 ?
	CP B
	RET Z
A1LOP:	INC HL			;FLYTTA TECKEN
	LD A,(HL)
	AND 7FH			;PARITET BORT
	LD (DE),A
	INC DE
	XOR A			;NOLLST[LL H\GA BYTEN
	LD (DE),A
	INC DE
	INC HL
	DJNZ A1LOP		;FLYTTA ALLA TECKEN
	RET
;_____________________________________________________
KAM2AS:
	;\VERS[TT A2 ARRAY TILL ADE ARRAY
	;  HL PEKAR P] ANTAL TECKEN
	;  DE PEKAR P] A2 ARRAY
	;  BC PEKAR P] ADE ARRAY
	;
	LD A,(HL)
	LD L,E
	LD H,D
	LD D,B
	LD E,C
	LD C,0
	LD B,A
	CALL A2ADE		;\VERS[TT
	RET
;__________________________________________________
A2ADE:
	;\VERS[TT A2 ARRAY TILL ADE ARRAY
	;  HL PEKAR P] A2 ARRAY
	;  DE PEKAR P] ADE ARRAY
	;  B ANGER ANTAL TECKEN >0 , <256
	;
	XOR A			;ANTAL TECKEN = 0 ?
	CP B
	RET Z
A2LOP:
	INC HL
	LD A,(HL)		;FLYTTA H\GA BYTEN
	AND 7FH			;PARITET BORT
	LD (DE),A
	INC DE
	XOR A
	LD (DE),A
	INC DE
	DEC HL
	LD A,(HL)		;L]GA BYTEN
	AND 7FH			;PARITET BORT
	LD (DE),A
	INC DE
	XOR A
	LD (DE),A
	INC DE
	INC HL
	INC HL
	DJNZ A2LOP		;FLYTTA ALLA TECKEN
	RET
;__________________________________________________
IYOBUF:	DEFS 13
UTBUFF:	DEFS 80
IYIBUF:	DEFS 13
INBUFF:	DEFS 80
